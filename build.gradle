plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "com.modrinth.minotaur" version "2.8.1"
    id "xyz.jpenilla.run-paper" version "2.1.0"
    id "maven-publish"
}

version = project.hasProperty('release') ? project.getProperty('release') : 'dev'

repositories {
    mavenCentral()
    maven {
        name = "Spigot"
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    mavenLocal()
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
    implementation project(path: ":mclogs-java", configuration: 'default')
    implementation "net.kyori:adventure-api:4.13.0"
    implementation "net.kyori:adventure-platform-bukkit:4.2.0"
}

processResources {
    inputs.property "version", project.version

    filesMatching("plugin.yml") {
        expand "version": project.version
    }
}

shadowJar {
    from sourceSets.main.allSource
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveFileName = "mclogs-bukkit-${project.version}.jar"
}

tasks {
    runServer {
        minecraftVersion("1.19.3")
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "6DdCzpTL"
    uploadFile = shadowJar
    gameVersions = [
            "1.20",
            "1.19.4",
            "1.19.3",
            "1.19.2",
            "1.19.1",
            "1.19",
            "1.18.2",
            "1.18.1",
            "1.18",
            "1.17.1",
            "1.17",
            "1.16.5",
            "1.16.4",
            "1.16.3",
            "1.16.2",
            "1.16.1",
            "1.16",
            "1.15.2",
            "1.15.1",
            "1.15",
            "1.14.4",
            "1.14.3",
            "1.14.2",
            "1.14.1",
            "1.14",
            "1.13.2",
            "1.13.1",
            "1.13",
    ]
    loaders = ["spigot", "paper", "purpur"]
    changelog = rootProject.file("CHANGELOG.md").text
}
